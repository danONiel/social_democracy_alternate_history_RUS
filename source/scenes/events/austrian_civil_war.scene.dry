title: Восстание в Австрии
new-page: true
subtitle: В ответ на нападения полиции австрийские социалисты поднимают восстание...
view-if: year = 1934 and (month = 2 or month = 3) and austrian_parliament_seen
max-visits: 1
on-arrival: austrian_civil_war_seen = 1
face-image: img/schutzbund.png
tags: event

= Восстание в Австрии

# pre-emptive democratic victory if sdapo strength >= 6
# long civil war if sdapo_strength = 5 or 4 - opportunity for interference/forced peace
# instant defeat if sdapo_strength < 4

Кризис в Австрии продолжается. "Австрофашистское" правительство во главе с Энгельбертом Дольфусом продолжает править на основании чрезвычайного положения. Оно преследует свою политическую оппозицию, в первую очередь социал-демократов из СДПА.

[? if sdapo_strength < 6: Сейчас Австрия вступает в состояние гражданской войны. Когда полиция попыталась отобрать оружие у Республиканского шуцбунда, Шуцбунд ответил применением силы, открыв огонь по правительственным войскам. По всей Австрии время от времени вспыхивают боевые действия между Шуцбундом и правительственными войсками. ?]

[? if sdapo_strength >= 6: Попытки правительства подавить социалистическую партию и Шуцбунд были встречены массовыми протестами, поддержанными СДПГ[? if foreign_minister_party = "SPD":  и Германским правительством?]. [? if eu_austria: Их также поддерживает недавно образованный Европейский союз, который ввел экономические санкции против правительства. ?] ?]

В конце концов...

- @crushed
- @long_war
- @sdapo_victory

@crushed
title: Мятежный Шуцбунд был разгромлен.
on-arrival: austria_defeat = 1
view-if: sdapo_strength < 4

Восстание продолжалось не более нескольких дней, прежде чем было подавлено. Шуцбунд и СДПА были слишком слабы и разобщены, чтобы противостоять силам государства.

@long_war
title: Это гражданская война.
view-if: sdapo_strength = 5 or sdapo_strength = 4
on-arrival: austria_civil_war = 1

Ни одна из сторон не отступает, и СДПА сформировала альтернативное правительство после победы над правительственными войсками в некоторых городах. Австрия находится в состоянии гражданской войны.

- @support_sdapo_party
- @support_sdapo_govt
- @force_peace
- @long_war_2

@support_sdapo_party
title: Как партия, наша партия окажет СДПА поддержку.
subtitle: -2 ресурсов
choose-if: resources >= 2
on-arrival: resources -= 2; sdapo_strength += 1
go-to: sdapo_victory if sdapo_strength >= 6; long_war_2 if sdapo_strength < 6

СДПГ мобилизует свои ресурсы для обеспечения СДПА оружием и добровольцами.

@support_sdapo_govt
title: Министерство иностранных дел будет поддерживать демократические силы Австрии.
view-if: spd_in_government and foreign_minister_party = "SPD"
on-arrival: sdapo_strength += 1
go-to: sdapo_victory if sdapo_strength >= 6; long_war_2 if sdapo_strength < 6

Мы оказываем дипломатическую и некоторую финансовую и материальную поддержку силам СДПА.

@force_peace
title: Можем ли мы объединить стороны для достижения мира?
view-if: spd_in_government and foreign_minister_party = "SPD" and sdapo_strength = 5
on-arrival: austria_peace = 1; pro_republic += 3; coup_progress -= 1
achievement: arbeiter_von_wien

Австрийское правительство, очевидно, считает нас предвзятыми, поэтому они ни за что не позволят нам провести мирные переговоры. Однако переговоры все равно начинаются. В Швейцарии, где фашистская Италия выступает в качестве защитника австрофашистов, а Германия - в качестве защитника СДПА, заключено мирное соглашение. Социалисты разоружены и отказываются от всех претензий на революцию, в то время как правые военизированные формирования также разоружены. Диктатуре приходит конец, и восстанавливается нормальное правительство со свободными выборами.

Учитывая стартовую позицию СДПА, это расценивается как победа. Теперь начинается длительная работа по реконструкции...

@long_war_2
title: Мы ничего не можем с этим поделать.
on-arrival: z_relation -= 6; dvp_relation -= 6; ddp_relation -= 3; catholics_spd -= 5; old_middle_spd -= 5; coalition_dissent += 1; economic_growth -= 0.5

Несмотря на все наши усилия, война продолжается. Унесено слишком много жизней. В Германию начинает прибывать поток беженцев, что негативно сказывается на торговле. Многие называют наше австрийское вмешательство ошибкой.

@sdapo_victory
title: Австрофашисты отступают!
on-arrival: pro_republic += 5; west_relation += 1; east_relation += 1; austria_victory = 1; reichswehr_loyalty += 0.1; coup_progress -= 2
view-if: sdapo_strength >= 6
achievement: arbeiter_von_wien

В результате австрофашисты отступили! Оказавшись в безвыходном положении внутри страны и за рубежом, они согласились восстановить Национальный совет и уйти из правительства, проведя новые выборы. Теперь начинается долгая работа по восстановлению. В результате наши страны, вероятно, еще больше сблизятся.

[? if eu_austria: Это большой успех нового Европейского союза, и его престиж, как видно, растет на международном уровне. ?]
