title: Австрийский таможенный союз
subtitle: Министерство иностранных дел стремится к более тесным связям с Австрией...
new-page: true
on-arrival: customs_union_seen = 1
tags: event
view-if: year = 1931 and month >= 3 and foreign_minister_party != "SPD" and foreign_minister_party != "KPD" and customs_union == 0 and not eu
max-visits: 1

# TODO: this should happen regardless of the current state of the government...

= Австрийский таможенный союз

[? if spd_in_government: министр иностранных дел из [+ foreign_minister_party +] предложил соглашение о таможенном союзе с Австрией. Мы можем либо принять, либо отклонить это соглашение. ?]
[? if not spd_in_government: правительство канцлера [+ chancellor +] предложило соглашение о таможенном союзе с Австрией. Скорее всего, это произойдет, что бы мы ни решили. ?]


Правые националисты давно мечтают о более тесных связях с Австрией, конечной целью которых является объединение двух немецкоговорящих стран. Таможенный союз с Австрией является первым шагом на пути к этой цели. Кроме того, многие считают, что союз с Австрией улучшил бы наши текущие экономические условия.

Однако западные страны, особенно Франция, скорее всего, будут возражать. Это соглашение нарушит условия Версальского договора и может привести к экономическим санкциям, что еще больше ударит по нашей экономике.

- @let_it_happen: Пусть это случится.
- @end_coalition: Остановите это!
- @end_toleration: Попытайтесь остановить это соглашение!

@let_it_happen
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

Таможенный союз был создан. Франция и другие соседние страны немедленно заявляют протесты. Долгосрочный отклик пока неизвестен.

@end_coalition
view-if: spd_in_government = 1
on-arrival: coalition_dissent += 1; dvp_relation -= 5; z_relation -= 4; z_relation -= 3 if chancellor_party == "Z"; dvp_relation -= 3 if chancellor_party == "Z"

Мы пытаемся донести до наших партнеров по коалиции серьезность потенциального ответа союзников. Предложение отвергнуто, хотя ННП и ПЦ недовольны.


@end_toleration
view-if: spd_toleration = 1
subtitle: Это сложно; все правоцентристские партии поддерживают эту политику.
go-to: end_toleration_success if spd_r + kpd_r + ddp_r >= 50; end_toleration_fail if spd_r + kpd_r + ddp_r < 50

@end_toleration_success
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;
call: set_next_election_time

С поддержкой КПГ и [+ ddp_name +], мы успешно остановили формирование таможенного союза. Это привело к падению правительства Канцлера [+ chancellor +].

Были назначены новые выборы на [+ next_election_month : month +] [+ next_election_year +].

@end_toleration_fail
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

К сожалению, крайне правые партии объединились с правительством, чтобы опередить нас в голосовании. Таможенный союз будет создан.
