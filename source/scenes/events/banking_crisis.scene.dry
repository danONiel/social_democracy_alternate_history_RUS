title: Банковский кризис
subtitle: Крах банковской системы Германии...
new-page: true
on-arrival: banking_crisis_seen = 1; workers_kpd += 5; workers_nsdap += 3; new_middle_nsdap += 3; old_middle_nsdap += 4; rural_nsdap += 3; unemployed += 3; crisis_urgency += 1; workers_spd -= 4 if spd_in_government; new_middle_spd -= 5 if spd_in_government; old_middle_spd -= 5 if spd_in_government; rural_spd -= 4 if spd_in_government; economic_growth -= 1;
tags: event
view-if: customs_union = 1 and banking_crisis_timer = 0
max-visits: 1
face-image: img/danat_bank.jpg

= Банковский кризис

Данатбанк рухнул! Вместе с ним рухнула и большая часть банковской системы Германии. Причина, по-видимому, кроется в стремлении правительства к таможенному союзу с Австрией, что фактически привело к международным санкциям.

Это буржуазный спор, но издержки понесет пролетариат. Люди сталкиваются с растущим голодом и нищетой, и, возможно, отчасти в этом виноваты мы.

- @nothing_to_do: Мы ничего не можем с этим поделать.
- @stabilization: Примите меры для стабилизации экономики!
- @austerity: Канцлер [+ chancellor +]'s ответил, как всегда, более жесткой экономией.
- @end_toleration: Мы устали от жесткой экономии. Прекратите политику терпимости!

@nothing_to_do
view-if: spd_in_government = 1
on-arrival: welfare -= 1; workers_spd -= 3; unemployed_spd -= 3; pro_republic -= 3

По сути, мы ничего не можем сделать.

@stabilization
view-if: spd_in_government = 1
subtitle: -1 budget
choose-if: budget >= 1
on-arrival: workers_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); unemployed -= 2; inflation += 1 if inflation < 0; budget -= 1; workers_nsdap -= 3; economic_growth += 0.9;

Перед лицом банковского кризиса мы предприняли экстренные меры для стабилизации экономики путем вливания денег в банковскую систему. Возможно, этого недостаточно, но это временно останавливает кризис.

@austerity
view-if: spd_in_government = 0
on-arrival: welfare -= 1; workers_spd -= 3; unemployed_spd -= 3; pro_republic -= 3; economic_growth -= 1;

Канцер [+ chancellor +] ответил срезами в системе социального обеспечения.

@end_toleration
view-if: spd_toleration = 1
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;
call: set_next_election_time

Мы устали от политики экономии Канцлера [+ chancellor +]. После попытки [+ chancellor +] использовать чрезвычайные полномочия для проведения очередного сокращения социальных выплат, мы сотрудничали с другими партиями, чтобы изменить эту политику. В ответ рейхстаг был распущен и назначены новые выборы.

Новые выборы были назначены на [+ next_election_month : month +] [+ next_election_year +].
