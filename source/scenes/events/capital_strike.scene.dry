title: Забастовка капитала
subtitle: Промышленность Германии простаивает...
view-if: (capital_strike_progress >= 10 or budget <= -5) and spd_in_government
new-page: true
max-visits: 1
on-arrival: unemployed += 5; workers_spd*= 0.7; new_middle_spd -= 15; old_middle_spd -= 15; unemployed_spd*=0.7; rural_spd -= 15; pro_republic -= 15; capital_strike_seen = 1; z_relation -= 10; dvp_relation -= 10; workers_nsdap += 8; unemployed_nsdap += 10; new_middle_nsdap += 15; old_middle_nsdap += 15; rural_nsdap += 15; economic_growth -= 4;
tags: event

= Забастовка капитала

Наша политика сильно разозлила капиталистов. Инвестиции сократились, заводы простаивают, что не позволяет рабочим выполнять свою работу. Тем временем буржуазная пресса постоянно выступает с ядовитыми нападками на нас и нашу экономическую политику.

Что мы можем сделать?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: Поощряйте рабочих захватывать фабрики!
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : Основная часть партии не поддерживает эту радикальную акцию. ?] [? if judicial_reform < 3 : Судебная власть выступит против этого действия. ?] [? if rb_militancy < 0.25 : Если бы мы увеличили численность наших военизированных формирований, возможно, мы смогли бы это сделать. ?] [? if works_councils < 1 : У нас не хватает поддержки профсоюзов. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; coup_progress += 6; unemployed -= 2; unemployed -= 1 if works_councils >= 3; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 4; workers_nsdap -= 8*(1-dissent); unemployed_nsdap -= 8*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; factory_takeovers += 1; economic_growth += 2;

Если капиталисты собираются напасть на нас, то мы должны дать им отпор. Мы дали возможность рабочим захватить и установить полный контроль над фабриками, которые капиталисты остановили. Фашисты, конечно, называют нас ворами и мародерами.


@capital_controls
title: Ввести контроль за движением капитала, чтобы уменьшить воздействие.
choose-if: judicial_reform >= 2
unavailable-subtitle: Судебная власть этого не допустит.
on-arrival: unemployed -= 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); economic_growth += 2;

Контроль за движением капитала может притупить некоторые из наихудших экономических последствий забастовки.



@propaganda
title: Разверните пропагандистскую кампанию, обвиняющую капиталистов в кризисе.
subtitle: -1 resources
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 :Мы недостаточно развили наши средства массовой информации. ?] [? if resources < 1: У нас недостаточно ресурсов. ?]
on-arrival: workers_spd += 7*(1-dissent); new_middle_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); pro_republic += 5*(1-dissent); resources -= 1

Мы запускаем контрпропагандистскую кампанию, направленную на то, чтобы обвинить капиталистов в экономическом кризисе, используя для этого все наши медиа-каналы. Из-за существующей структуры наших средств массовой информации это довольно успешно направляет популярную критику в сторону капиталистов, а не в нашу сторону.


@nothing
title: Мы ничего не можем сделать.

Мы ничего не можем сделать в рамках конституции и закона.
